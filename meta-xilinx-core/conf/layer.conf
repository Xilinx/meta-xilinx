# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a packages directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb"
BBFILES += "${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "xilinx"
BBFILE_PATTERN_xilinx = "^${LAYERDIR}/"
BBFILE_PRIORITY_xilinx = "5"

BBFILES_DYNAMIC += " \
openembedded-layer:${LAYERDIR}/dynamic-layers/openembedded-layer/recipes-*/*/*.bb \
openembedded-layer:${LAYERDIR}/dynamic-layers/openembedded-layer/recipes-*/*/*.bbappend \
meta-python:${LAYERDIR}/dynamic-layers/meta-python/recipes-*/*/*.bb \
meta-python:${LAYERDIR}/dynamic-layers/meta-python/recipes-*/*/*.bbappend \
openamp-layer:${LAYERDIR}/dynamic-layers/openamp-layer/recipes-*/*/*.bb \
openamp-layer:${LAYERDIR}/dynamic-layers/openamp-layer/recipes-*/*/*.bbappend \
virtualization-layer:${LAYERDIR}/dynamic-layers/virtualization-layer/recipes-*/*/*.bb \
virtualization-layer:${LAYERDIR}/dynamic-layers/virtualization-layer/recipes-*/*/*.bbappend \
xilinx-tools:${LAYERDIR}/dynamic-layers/meta-xilinx-tools/recipes-*/*/*.bb \
xilinx-tools:${LAYERDIR}/dynamic-layers/meta-xilinx-tools/recipes-*/*/*.bbappend \
"

LAYERDEPENDS_xilinx = "core meta-arm"
LAYERRECOMMENDS_xilinx = "openembedded-layer"

LAYERSERIES_COMPAT_xilinx = "scarthgap"

SIGGEN_EXCLUDE_SAFE_RECIPE_DEPS += " \
  *->xrt \
  *->zocl \
  *->cairo \
  *->libepoxy \
  *->gstreamer1.0-plugins-base \
  *->gtk+ \
  *->gtk+3 \
  *->libglu \
  *->libsdl \
  *->libsdl2 \
  *->qemu \
  *->xserver-xorg \
"

XILINX_RELEASE_VERSION ??= "v2024.2"

BUILDCFG_VARS:append = " XILINX_RELEASE_VERSION"

DEFAULT_XILINX_QEMU = "qemu-xilinx"
DEFAULT_XILINX_QEMU:arm = "qemu"
PREFERRED_PROVIDER_qemu ?= "${DEFAULT_XILINX_QEMU}"

XILINX_ATF_VERSION[v2022.1] = "2.6-xilinx-v2022.1%"
XILINX_ATF_VERSION[v2022.2] = "2.6-xilinx-v2022.2%"
XILINX_ATF_VERSION[v2023.1] = "2.8-xilinx-v2023.1%"
XILINX_ATF_VERSION[v2023.2] = "2.8-xilinx-v2023.2%"
XILINX_ATF_VERSION[v2024.1] = "2.10-xilinx-v2024.1%"
XILINX_ATF_VERSION[v2024.2] = "2.10-xilinx-v2024.2%"
PREFERRED_VERSION_virtual/arm-trusted-firmware ?= "${@d.getVarFlag('XILINX_ATF_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

# The name of the software has changed to match upstream ARM
XILINX_ATF_PROVIDERS[v2024.2] = "trusted-firmware-a"
PREFERRED_PROVIDER_virtual/arm-trusted-firmware = "${@d.getVarFlag('XILINX_ATF_PROVIDERS', d.getVar('XILINX_RELEASE_VERSION')) or 'arm-trusted-firmware'}"


XILINX_UBOOT_VERSION[v2022.1] = "1:2021.01-xilinx-v2022.1%"
XILINX_UBOOT_VERSION[v2022.2] = "1:2022.01-xilinx-v2022.2%"
XILINX_UBOOT_VERSION[v2023.1] = "1:2023.01-xilinx-v2023.1%"
XILINX_UBOOT_VERSION[v2023.2] = "1:2023.01-xilinx-v2023.2%"
XILINX_UBOOT_VERSION[v2024.1] = "1:2024.01-xilinx-v2024.1%"
XILINX_UBOOT_VERSION[v2024.2] = "1:2024.01-xilinx-v2024.2%"

PREFERRED_VERSION_u-boot-xlnx ?= "${@d.getVarFlag('XILINX_UBOOT_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_u-boot-tools-xlnx ?= "${@d.getVarFlag('XILINX_UBOOT_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

XILINX_LINUX_VERSION[v2022.1] = "5.15.19%"
XILINX_LINUX_VERSION[v2022.2] = "5.15.36%"
XILINX_LINUX_VERSION[v2023.1] = "6.1.30%"
XILINX_LINUX_VERSION[v2023.2] = "6.1.60%"
XILINX_LINUX_VERSION[v2024.1] = "6.6.10%"
XILINX_LINUX_VERSION[v2024.2] = "6.6.40%"
PREFERRED_VERSION_linux-xlnx ?= "${@d.getVarFlag('XILINX_LINUX_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

# XRT/ZOCL
XRT_ZOCL_VERSION[v2022.1] = "202210.2.13.479"
XRT_ZOCL_VERSION[v2022.2] = "202220.2.14.0"
XRT_ZOCL_VERSION[v2023.1] = "202310.2.15.0"
XRT_ZOCL_VERSION[v2023.2] = "202320.2.16.0"
XRT_ZOCL_VERSION[v2024.1] = "202410.2.17.319"
XRT_ZOCL_VERSION[v2024.2] = "202420.2.18.0"
PREFERRED_VERSION_xrt ?= "${@d.getVarFlag('XRT_ZOCL_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_zocl ?= "${@d.getVarFlag('XRT_ZOCL_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

# AI-Engine
AIEFAL_VERSION[v2022.1] = "1.4"
AIEFAL_VERSION[v2022.2] = "1.4"
AIEFAL_VERSION[v2023.1] = "1.5"
AIEFAL_VERSION[v2023.2] = "1.5"
AIEFAL_VERSION[v2024.1] = "1.6"
AIEFAL_VERSION[v2024.2] = "1.7"
PREFERRED_VERSION_aiefal ?= "${@d.getVarFlag('AIEFAL_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

AI_ENGINE_DRIVER_VERSION[v2022.1] = "3.3"
AI_ENGINE_DRIVER_VERSION[v2022.2] = "3.3"
AI_ENGINE_DRIVER_VERSION[v2023.1] = "3.4"
AI_ENGINE_DRIVER_VERSION[v2023.2] = "3.4"
AI_ENGINE_DRIVER_VERSION[v2024.1] = "3.5"
AI_ENGINE_DRIVER_VERSION[v2024.2] = "3.6"
PREFERRED_VERSION_ai-engine-driver ?= "${@d.getVarFlag('AI_ENGINE_DRIVER_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

# Add support to eSDK for gen-machine-conf if it exists
PLNX_SCRIPTS_PATH = "${LAYERDIR}/gen-machine-conf/gen-machine-scripts"
BB_HASHEXCLUDE_COMMON:append = " PLNX_SCRIPTS_PATH"

IMAGE_CLASSES += "gen-machine-conf"
