# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a packages directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb"
BBFILES += "${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "xilinx"
BBFILE_PATTERN_xilinx = "^${LAYERDIR}/"
BBFILE_PRIORITY_xilinx = "5"

BBFILES_DYNAMIC += " \
openembedded-layer:${LAYERDIR}/dynamic-layers/openembedded-layer/recipes-*/*/*.bb \
openembedded-layer:${LAYERDIR}/dynamic-layers/openembedded-layer/recipes-*/*/*.bbappend \
chromium-browser-layer:${LAYERDIR}/dynamic-layers/chromium-browser-layer/recipes-*/*/*.bb \
chromium-browser-layer:${LAYERDIR}/dynamic-layers/chromium-browser-layer/recipes-*/*/*.bbappend \
meta-python:${LAYERDIR}/dynamic-layers/meta-python/recipes-*/*/*.bb \
meta-python:${LAYERDIR}/dynamic-layers/meta-python/recipes-*/*/*.bbappend \
virtualization-layer:${LAYERDIR}/dynamic-layers/virtualization-layer/recipes-*/*/*.bb \
virtualization-layer:${LAYERDIR}/dynamic-layers/virtualization-layer/recipes-*/*/*.bbappend \
xilinx-tools:${LAYERDIR}/dynamic-layers/meta-xilinx-tools/recipes-*/*/*.bb \
xilinx-tools:${LAYERDIR}/dynamic-layers/meta-xilinx-tools/recipes-*/*/*.bbappend \
"

LAYERDEPENDS_xilinx = "core"
LAYERRECOMMENDS_xilinx = "openembedded-layer chromium-browser-layer"

LAYERSERIES_COMPAT_xilinx = "kirkstone"

SIGGEN_EXCLUDE_SAFE_RECIPE_DEPS += " \
  *->zocl \
  *->cairo \
  *->libepoxy \
  *->gstreamer1.0-plugins-base \
  *->gtk+ \
  *->gtk+3 \
  *->libglu \
  *->libsdl \
  *->libsdl2 \
  *->mesa \
  *->mesa-gl \
  *->qemu \
  *->virglrenderer \
  *->xserver-xorg \
"

XILINX_RELEASE_VERSION ??= "v2022.2"

XILINX_DEPRECATED[meta-xilinx] = "Kirkstone is not officially supported.  See the meta-xilinx/README.kirkstone file for more information."

BUILDCFG_VARS:append = " XILINX_RELEASE_VERSION"

PREFERRED_VERSION_qemu-xilinx ?= "v6.1.0-xilinx-${XILINX_RELEASE_VERSION}%"
PREFERRED_VERSION_qemu-xilinx-native ?= "v6.1.0-xilinx-${XILINX_RELEASE_VERSION}%"
PREFERRED_VERSION_qemu-xilinx-system-native ?= "v6.1.0-xilinx-${XILINX_RELEASE_VERSION}%"
PREFERRED_VERSION_qemu-devicetrees ?= "xilinx-${XILINX_RELEASE_VERSION}%"
PREFERRED_VERSION_arm-trusted-firmware ?= "2.6-xilinx-${XILINX_RELEASE_VERSION}%"
PREFERRED_VERSION_u-boot-xlnx ?= "v2021.01-xilinx-${XILINX_RELEASE_VERSION}%"
PREFERRED_VERSION_pmu-rom-native ?= "${@d.getVar("XILINX_RELEASE_VERSION").replace('v','')}"
PREFERRED_VERSION_linux-xlnx ?= "${@'5.15.19' if d.getVar("XILINX_RELEASE_VERSION") == 'v2022.1' else '5.15.36'}%"

# Weak defaults to avoid seeing redundant multiple provider notices
PREFERRED_PROVIDER_nativesdk-qemu ??= "nativesdk-qemu"
PREFERRED_PROVIDER_qemu-helper-native ??= "qemu-helper-native"
PREFERRED_PROVIDER_qemu-native ??= "qemu-native"
PREFERRED_PROVIDER_qemu-system-native ??= "qemu-system-native"
